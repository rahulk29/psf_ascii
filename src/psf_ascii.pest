WHITESPACE = _{ " " | "\t" | "\r" | "\n" }

psf_ascii = _{ SOI ~ psf_ascii_inner ~ EOI }
psf_ascii_inner = {
    header_section
    ~ (type_section ~ sweep_section ~ trace_section ~ value_section)?
    ~ end
}

header_section = {
    "HEADER"
    ~ named_values
}

named_values = { (named_value)* }
named_value = { string ~ value }
value = _{ string | real_value | integer }
real_value = _{ real | nan }
nan = { "nan" | "NaN" }

type_section = {
    "TYPE"
    ~ types
}

types = { (type_decl)* }

type_decl = { string ~ kinds }

kinds = { (kind)+ }

kind = {
    ("FLOAT" | "DOUBLE" | "COMPLEX" | "INT" | "BYTE" | "LONG" | "STRING" | array | struct_decl | prop | star)
}

struct_decl = { "STRUCT" ~ "(" ~ types ~ ")" }
array = { "ARRAY" ~ "(" ~ star ~ ")" }
prop = { "PROP" ~ "(" ~ named_values ~ ")" }
star = { "*" }

sweep_section = { "SWEEP" ~ sweeps }
sweeps = { (sweep)* }
sweep = { string ~ string ~ kinds }

trace_section = { "TRACE" ~ traces }
traces = { (trace)+ }
trace = { 
    named_value
    | (string ~ "GROUP" ~ integer)
    | (named_value ~ prop)
}

value_section = { "VALUE" ~ values }
values = { (signal_value)+ }
signal_value = {
    (string ~ numbers)
    | (string ~ string ~ numbers)
}

numbers = _{ simple_numbers | composite_numbers }
simple_numbers = { (simple_number)+ }
simple_number = _{
    integer
    | real
    | (real ~ prop)
    | nan
    | (nan ~ prop)
}
composite_numbers = { (composite_number)+ }
composite_number = {
    ("(" ~ simple_numbers ~ ")")
    | ("(" ~ simple_numbers ~ ")" ~ prop)
}


end = _{ "END" }


real = @{
    ("+" | "-")?
    ~ (ASCII_DIGIT)+
    ~ "."
    ~ (ASCII_DIGIT)*
    ~ (^"e" ~ ("+" | "-")? ~ ASCII_DIGIT+)?
}

integer = @{
    "-"?
    ~ (ASCII_DIGIT)+
}

string = ${ "\"" ~ string_inner ~ "\"" }
string_inner = @{ string_char* }
string_char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

